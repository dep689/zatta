1 CLS:M=100:B=10:X=0:Y=0:Z=0

2 IF M=0 GOTO 29
3 B=B+10*BTN(UP)-10*BTN(DOWN)
4 IF B<10 B=10
5 IF B>M  B=M
6 IF BTN(SPACE) GOTO 8
7 GOSUB 17:GOTO 2

8 M=M-B:L=0:D=0:R=0:X=RND(10):Y=RND(10):Z=RND(10)
9 L=L|BTN(LEFT):D=D|BTN(DOWN):R=R|BTN(RIGHT)
10 IF L=0 X=(X+1)%10
11 IF D=0 Y=(Y+1)%10
12 IF R=0 Z=(Z+1)%10
13 GOSUB 17
14 IF L*D*R=0 GOTO 9
15 IF X=Y AND X=Z THEN GOSUB 24
16 GOTO 2

17 CLS
18 LOCATE 0,0:PRINT"MONEY",M,"BET",B
19 LOCATE 0,2:PRINT"PRESS SPACE TO START"
20 LOCATE 0,3:PRINT"PRESS ← ↓ → TO STOP"
21 LOCATE 13,10:PRINT X,Y,Z
22 LOCATE 13,12:PRINT"← ↓ →"
23 RETURN

24 LED1:WAIT10:LED0:WAIT10:LED1:WAIT10:LED0:WAIT10:LED1:WAIT10:LED0
25 P=10*B
26 M=M+10:P=P-10
27 GOSUB 17
28 IF P=0 THEN RETURN ELSE GOTO 26

29 LOCATE 0,15:PRINT "GAME OVER"
30 CLK:END
